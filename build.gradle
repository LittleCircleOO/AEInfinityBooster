plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.142'
    id 'net.darkhax.curseforgegradle' version '1.1.15'
    id "com.modrinth.minotaur" version "2.+"
}

def env = System.getenv()
if (env.GITHUB_RUN_NUMBER) {
    mod_version = 'neoforge-1.20.6-1.0.1' + "." + "${env.GITHUB_RUN_NUMBER}"
}

version = mod_version
group = mod_group_id

repositories {
    mavenLocal()
    maven {
        name "Modmaven"
        url "https://modmaven.dev/"
        content {
            includeGroup 'appeng'
        }
    }
    maven {
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        // location of the maven that hosts JEI files since January 2023
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
}

base {
    archivesName = mod_id
}

// Mojang ships Java 21 to end users starting in 1.20.5, so mods should target Java 21.
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'

        modSource project.sourceSets.main
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
        programArgument '--nogui'
    }

    gameTestServer {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    data {
        programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    runtimeOnly "mezz.jei:jei-1.20.6-neoforge:18.0.0.62"
    compileOnly "appeng:appliedenergistics2-neoforge:18.1.3-alpha"
    runtimeOnly "appeng:appliedenergistics2-neoforge:18.1.3-alpha"
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }
}


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {

    apiToken = env.CURSE_API_KEY

    // The main file to upload
    def mainFile = upload(559313, jar)
    mainFile.releaseType = 'release'
    mainFile.changelog = 'A changelog can be found at https://github.com/Hexeption/AEInfinityBooster'
    mainFile.changelogType = 'markdown'
    mainFile.gameVersions = ['1.20.6']
    mainFile.addModLoader("neoforge")
    mainFile.displayName = "AEInfinityBooster ${version}"


}

modrinth {
    token = env.MODRINTH_API_KEY
    projectId = 'VQhDBNs8'
    versionName = "AEInfinityBooster $version"
    changelog = 'A changelog can be found at https://github.com/Hexeption/AEInfinityBooster'
    uploadFile = jar
    gameVersions = ["1.20.6"]
    loaders = ["neoforge"]
    dependencies {
        required.version "ae2", "18.1.3-alpha"
    }
}
